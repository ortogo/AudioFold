class Config {

   def Config(path, defLog) {
      this.defLog = defLog
      this.path = path
      this.init()
   }

   def set(key, value) {
      this[key] = value
   }

   def init() {
      if !exists(this.path) {
         // записываем пустой конфиг
         this.unpackFields(CONFIG_TEMPLATE)
         this.save()
         return 0
      }
      // читаем конфиг
      configFile = fopen(this.path, "r")
      jsonString = readText(configFile)
      fclose(configFile)

      jsonArray = jsondecode(jsonString)
      this.unpackFields(jsonArray)
   }

   def unpackFields(jsonArray) {
      for key, value : jsonArray {
         this[key] = trim(value)
      }
   }

   def packFields() {
      newConfig = CONFIG_TEMPLATE
      for key, value : newConfig {
         newConfig[key] = trim(this[key])
      }
      return newConfig
   }

   def save() {
      this.defLog("Saving config...")
      newConfig = this.packFields()
      jsonString = jsonencode(newConfig)
      configFile = fopen(this.path, "w")
      writeLine(configFile, jsonString)
      fclose(configFile)
      this.defLog("Config saved!")
   }

   def check() {
      if trim(this.botToken) == "" {
         this.defLog("Error: bot token is not set!!!")
      }
      if trim(this.username) == "" {
         this.defLog("Error: your username is not set!!!")
      }
      if trim(this.saveFolderPath) == "" {
         this.defLog("Error: save folder path is not set!!!")
      }
      if !exists(this.saveFolderPath) {
         this.defLog("Error: save folder path [" + this.saveFolderPath + "] is not exists!!!")
      }
   }
}

